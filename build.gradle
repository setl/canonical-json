import com.github.spotbugs.SpotBugsTask

buildscript {
  repositories {
    maven { url "http://si-nexus01.dev.setl.io:8081/repository/maven-public/" }
  }
  dependencies {
    classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
  }
}

plugins {
  id "com.github.spotbugs" version "2.0.0"
}

apply plugin: 'java'

apply plugin: 'com.github.spotbugs'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'

group 'io.setl'
version '2.0'
sourceCompatibility = 11
targetCompatibility = 11

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

checkstyle {
  toolVersion = "7.8.2"
  configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

spotbugs {
  toolVersion = '3.1.12'
  sourceSets = [sourceSets.main]
  ignoreFailures = true
  reportsDir = file("$project.buildDir/findbugsReports")
}

task findSecurityBugs(type: SpotBugsTask) {
  classes = fileTree(project.rootDir.absolutePath).include("**/*.class")
  source = fileTree(project.rootDir.absolutePath).include("**/*.java")
  classpath = files()
  pluginClasspath = project.configurations.spotbugsPlugins
}

jacoco {
  toolVersion = "0.8.5"
}

repositories {
  //Allow local repository for snapshot builds
  if (version.endsWith("SNAPSHOT")) {
    mavenLocal()
  }
  maven { url "http://si-nexus01.dev.setl.io:8081/repository/maven-public/" }
  mavenCentral()
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

dependencies {
  compile group: 'javax.json', name: 'javax.json-api', version: '1.1.4'

  // We need this library for the Longest Common Subsequence solver which is needed for creating patches
  compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'

  compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
  compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.9'
  compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9'
  compileOnly group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: '2.2.4.RELEASE'
  compileOnly group: 'org.springframework', name: 'spring-webmvc', version: '5.2.3.RELEASE'
  compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.2.0'

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
  testCompile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-smile', version: '2.10.2'
  testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.9'
  testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9'

  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.0'
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier "sources"
      }
    }
  }
  repositories {
    maven {
      credentials {
        username = 'publisher'
        password = 'publish'
      }
      if (version.endsWith("SNAPSHOT")) {
        url 'http://si-nexus01.dev.setl.io:8081/repository/setl-snaps'
      } else {
        url 'http://si-nexus01.dev.setl.io:8081/repository/setl-libs'
      }
    }
  }
}

